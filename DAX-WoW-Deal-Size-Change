/*
This formula tells the user what the Week Over Week (WoW) change for each deal has been. 
For example, if Business XYZ had a dollar value of $283,112 last week and this week it has a dollar value of $321,112, then it will display $38,000. 
By using conditional formatting in Power BI one can make it easy for the user to see whether it's a negative, neutral, or positive change.
Formula fields and values have been edited to preserve Adobe's confidentiality. 
*/

Metric_Change = 
SUMX(
    'Data_Table',
    VAR Prev_Status = 'Data_Table'[Previous_Period_Status]
    VAR Curr_Status = 'Data_Table'[Current_Period_Status]
    VAR Is_Shifted_Out = 'Data_Table'[Is_Shifted_Out]
    VAR Is_Shifted_In = 'Data_Table'[Is_Shifted_In]
    VAR Is_New = ISBLANK('Data_Table'[Previous_Period_Date])
    VAR Same_Period = 'Data_Table'[Previous_Period_Date] = 'Data_Table'[Current_Period_Date]
    VAR Value_Change = [Current_Value] - [Previous_Value]
    RETURN
    SWITCH(
        TRUE(),
        -- Deals Moved Out (Active or Likely to Inactive/Lost/Excluded) (-)
        (Prev_Status IN {"Active", "Likely"}) && 
        (Is_Shifted_Out || ISBLANK('Data_Table'[Category]) || 'Data_Table'[Exclusion_Flag] <> 0),
        -[Previous_Value],

        -- New or Shifted-In Deals to Active or Likely (+)
        (Is_New || Is_Shifted_In) && Curr_Status IN {"Active", "Likely"},
        [Current_Value],

        -- Status Changes: Low-Priority to Active/Likely (+)
        Prev_Status IN {"Low-Priority", "Low-Priority-Tracked"} && 
        Curr_Status IN {"Active", "Likely"},
        [Current_Value],

        -- Status Changes: Active to Low-Priority (-)
        Prev_Status = "Active" && 
        Curr_Status IN {"Low-Priority", "Low-Priority-Tracked"},
        -[Previous_Value],

        -- Value Changes in Same Period (+ or -)
        Same_Period && Value_Change <> 0 && 
        Curr_Status IN {"Active", "Likely"},
        Value_Change,

        BLANK()
    )
)

/*
Output example. The formula above is reponsible for the "WoW Change: column. 
*/

| ID       | LW Period | Current Period | LW Stage | Stage | LW Status | Current Status | Account | License Type | LW Value | Current Value | WoW Change | Change Reason   |
|----------|-----------|----------------|----------|-------|-----------|----------------|---------|---------------|----------|---------------|------------|-----------------|
| REC001   | P1        | P2             | S1       | S1    | Forecast  | Forecast       | Client A| New           | $500     | $700          | +$200      | Value Adjustment|
| REC002   | P1        | P2             | S2       | S2    | Forecast  | Forecast       | Client B| Renewal       | $400     | $300          | -$100      | Value Adjustment|
| REC003   | P1        | P2             | S3       | S3    | Forecast  | Forecast       | Client C| Renewal       | $1,000   | $900          | -$100      | Value Adjustment|
| REC004   | P1        | P2             | S4       | S4    | Forecast  | Forecast       | Client D| New           | $300     | $500          | +$200      | Value Adjustment|
