/*
This DAX formula tells the user what the Week Over Week (WoW) change for each deal has been. 
For example, if Business XYZ had a dollar value of $283,112 last week and this week it has a dollar value of $321,112, then it will display $38,000. 
By using conditional formatting in Power BI one can make it easy for the user to see whether it's a negative, neutral, or positive change.
Formula fields and values have been edited to preserve Adobe's confidentiality. 
*/

Metric_Change = 
SUMX(
    'Data_Table',
    VAR Prev_Status = 'Data_Table'[Previous_Period_Status]
    VAR Curr_Status = 'Data_Table'[Current_Period_Status]
    VAR Is_Shifted_Out = 'Data_Table'[Is_Shifted_Out]
    VAR Is_Shifted_In = 'Data_Table'[Is_Shifted_In]
    VAR Is_New = ISBLANK('Data_Table'[Previous_Period_Date])
    VAR Same_Period = 'Data_Table'[Previous_Period_Date] = 'Data_Table'[Current_Period_Date]
    VAR Value_Change = [Current_Value] - [Previous_Value]
    RETURN
    SWITCH(
        TRUE(),
        -- Deals Moved Out (Active or Likely to Inactive/Lost/Excluded) (-)
        (Prev_Status IN {"Active", "Likely"}) && 
        (Is_Shifted_Out || ISBLANK('Data_Table'[Category]) || 'Data_Table'[Exclusion_Flag] <> 0),
        -[Previous_Value],

        -- New or Shifted-In Deals to Active or Likely (+)
        (Is_New || Is_Shifted_In) && Curr_Status IN {"Active", "Likely"},
        [Current_Value],

        -- Status Changes: Low-Priority to Active/Likely (+)
        Prev_Status IN {"Low-Priority", "Low-Priority-Tracked"} && 
        Curr_Status IN {"Active", "Likely"},
        [Current_Value],

        -- Status Changes: Active to Low-Priority (-)
        Prev_Status = "Active" && 
        Curr_Status IN {"Low-Priority", "Low-Priority-Tracked"},
        -[Previous_Value],

        -- Value Changes in Same Period (+ or -)
        Same_Period && Value_Change <> 0 && 
        Curr_Status IN {"Active", "Likely"},
        Value_Change,

        BLANK()
    )
)

-----------------------------
/*

OUTPUT EXAMPLE BY DEAL ID

The formula above is reponsible for the "WoW Change" column. Note that WoW Change displays $75K in the first row even though the dollar value of the opportunity didn't change. That's because once it moves into Change Reason Forecast/Won, it couunts as a positive addition to this week's forecast. 

| Deal ID | Current Quarter | Stage  | Current Status | Client   | LW Gross ASV | Gross ASV | WoW Change | Change Reason                |
|---------|-----------------|--------|----------------|----------|--------------|-----------|------------|------------------------------|
| Deal001 | Q1 2022         | Stage2 | Forecast       | Client A | $75,000      | $75,000   | $75,000    | Upside/UT -> Forecast/Won    |
| Deal002 | Q1 2022         | Stage2 | Forecast       | Client B | $48,000      | $48,000   | $48,000    | Upside/UT -> Forecast/Won    |
| Deal003 | Q1 2022         | Stage1 | Forecast       | Client C | $220,000     | $280,000  | $60,000    | Deal Size Change             |
| Deal004 | Q1 2022         | Stage3 | Forecast       | Client D | $130,000     | $170,000  | $40,000    | Deal Size Change             |
| Deal005 | Q1 2022         | Stage2 | Forecast       | Client E | $0           | $62,000   | $62,000    | New or Pulled In to Forecast |
| Deal006 | Q2 2022         | -      | -              | Client F | $85,000      | $0        | -$85,000   | Won/Forecast -> Push/Lost    |
| Deal007 | Q1 2022         | Stage2 | Upside         | Client G | $300,000     | $300,000  | -$300,000  | Forecast -> Upside/UT        |
| Deal008 | Q1 2022         | Stage4 | Won            | Client H | $0           | $33,000   | $33,000    | New or Pulled In to Won      |
| Deal009 | Q2 2022         | -      | -              | Client I | $50,000      | $0        | -$50,000   | Won/Forecast -> Push/Lost    |
| Deal010 | Q1 2022         | Stage4 | Forecast       | Client J | $25,000      | $20,000   | -$5,000    | Deal Size Change             |

OUTPUT EXAMPLE BY PRODUCT

Solution Category | Previous ASV | Current ASV | Month Change
------------------|--------------|-------------|-------------
Category A        | $10,000,000  | $9,500,000  | ($500,000)
Category B        | $4,000,000   | $5,200,000  | $1,200,000
Category C        | $11,000,000  | $10,500,000 | ($500,000)
Category D        | $3,500,000   | $4,700,000  | $1,200,000
Total             | $28,500,000  | $29,900,000 | $1,400,000

OUTPUT EXAMPLE BY FRONT LINE MANAGER

By FLM | WoW Change
-------|------------
Name A | ($120,000)
Name B | $90,000
Name C | ($80,000)
Name D | $150,000
Name E | ($60,000)
Name F | $200,000
Name G | ($300,000)
Total  | $1,280,000

*/
